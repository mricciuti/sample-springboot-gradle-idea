// Plugins 
plugins {
    id "java"
    id "org.springframework.boot" version "2.1.4.RELEASE"
    id "idea"
}
apply plugin: "io.spring.dependency-management"

// General config
group "github.mri.sample"

// Java configuration
sourceCompatibility = "1.8"
[compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

// Dependencies
repositories {
    jcenter()
}
ext {
    // path to Gradle generated main sources directory
    gradleGeneratedMainSourcesDir = "$buildDir/generated/sources/annotationProcessor/java/main/"
    // path to Gradle generated test sources directory
    gradleGeneratedTestSourcesDir = "$buildDir/generated/sources/annotationProcessor/java/test/"
    // path to IDEA generated sources directory
    ideaGeneratedSourcesDir = "$projectDir/out/production/classes/generated"

    // Deps versions
    lombokVersion = "1.18.6"
    mapstructVersion = "1.2.0.Final"
    hibernateJpaVersion = '1.0.2.Final'
}

dependencies {
    /* -------------- Spring Boot deps--------------------------------------------*/
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    /* -------------- Lombok -----------------------------------------------------*/
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    /* -------------- Db / persistence --------------------------------------------*/
    implementation("com.h2database:h2")

    /* ------------ Mapstruct ----------------------------------------------------*/
    compileOnly("org.mapstruct:mapstruct-jdk8:${mapstructVersion}")
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    /* ------------------ Test dependencies --------------------------------------*/
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

// IDEA plugin configuration (for sources dirs setup)
idea {
    module {
        // exclude main & test sources generated by Gradle from project source directories
        excludeDirs += file(gradleGeneratedMainSourcesDir)
        excludeDirs += file(gradleGeneratedTestSourcesDir)

        // include generated sources directory managed by IDEA
        sourceDirs += file(ideaGeneratedSourcesDir)
        generatedSourceDirs += file(ideaGeneratedSourcesDir)
    }
}

compileJava {    
    options.annotationProcessorPath = configurations.annotationProcessor
}
